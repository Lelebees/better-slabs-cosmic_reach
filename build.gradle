import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = project.mod_version
group = project.maven_group

base {
    archivesName = "$mod_name-$version-Fabric-$loader_version-for-$cosmicreach_version"
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://repo.spongepowered.org/maven/"
    }
    maven {
        url "https://maven.fabricmc.net/"
    }
}

dependencies {
    //Cosmic Reach main jar
    implementation(files("$project.rootDir/run/cosmic-reach.jar"))
//    implementation fileTree(dir: "$project.rootDir/run/deps", includes: ['*.jar'])
    implementation project(':modLoader')
//    implementation annotationProcessor("io.github.llamalad7:mixinextras-fabric:$mixin_extras_version")
}

processResources {
    inputs.property "version", project.version
    inputs.property "loader_version", project.loader_version
    inputs.property "cosmicreach_version", project.cosmicreach_version
    inputs.property "mod_name", project.mod_name
    inputs.property "modid", project.modid
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "loader_version": project.loader_version,
                "cosmicreach_version": project.cosmicreach_version,
                "mod_name": project.mod_name,
                "modid": project.modid
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

tasks.register('copyDependencies', Copy) {
    group 'build'
    from configurations.runtimeClasspath
    into "build/libs/deps/"
}

assemble {
    group 'build'
    dependsOn 'jar'
    dependsOn 'copyDependencies'
}

tasks.register('buildAndCopy', Copy) {
    group 'build'
    dependsOn 'assemble'
    from "build/libs/"
    into "../run/mods"
}

tasks.register('runClient', Exec) {
    //TODO: actually add stuff to classpath instead of compiling and copying.
    dependsOn 'buildAndCopy'
    workingDir "${project.rootDir}/run/"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', "${project.rootDir}/run/launch.bat"
    } else {
        commandLine 'sh', "${project.rootDir}/run/launch.sh"
    }
}